apply plugin: 'com.intershop.gradle.scmversion'
scm {
	version {
		type = 'threeDigits'
		increment = 'PATCH'
		patternDigits = 1
		initialVersion = '1.0.0'
        disableSCM = true
		// activate this, if your branch name does not contain
    	// a version part, eg. BB_ISTOOLS-3119
    	// if your branch contains a version (eg BB_14-ISTOOLS-4711)
    	// set the following property to true
        branchWithVersion = false
	}
}

version = scm.version.version

apply plugin: 'com.intershop.gradle.versionrecommender'
apply plugin: 'hot-code-reloading'
apply plugin: 'ivy-publish'

// ci configuration
/**
 * Simple Release configuration
 * requires only a Maven compatible repository and
 * additional environment variables.
 * See also https://github.com/IntershopCommunicationsAG/gradle-release-plugins. 
 **/
apply plugin: 'com.intershop.gradle.simplepublish-configuration'

description 'Ivable Common Components'
group = 'com.ivable.common'

assert System.properties['java.version'].startsWith('1.8')

versionRecommendation {
    provider {
        // can be activated with a special version
        ivy('infrastructure',  'com.intershop.infrastructure:assembly') {}
        ivy('platform',  'com.intershop.platform:platform') {}
        ivy('content',  'com.intershop.content:assembly') {}
        ivy('business',  'com.intershop.business:intershop7') {}
        ivy('b2b',  'com.intershop.b2b:b2b') {}

        // default dependencies
        // ivy('icm-b2c', 'com.intershop.assembly:commerce_management_b2c:7.9.0.0') {}
        ivy('icm-b2x', 'com.intershop.assembly:commerce_management_b2x:7.9.0.0') {}

        // intershop build dependencies
        properties('intershopBuild', file('intershopBuild.version')) {}
		
		// consume version filters 
		pom('versionfilter-icm', 'com.intershop.versions:versionfilter-icm:2.0.0') {}
		pom('versionfilter-icm-test', 'com.intershop.versions:versionfilter-icm-test:1.0.4') {}
    }
    
    // all versions are used from providers
    forceRecommenderVersion = true

    excludeProjectsbyName = ['']

    updateConfiguration {
        // Necessary for update of ivy artifacts
        ivyPattern = com.intershop.gradle.repoconfig.RepoConfigRegistry.ivyPattern

        defaultUpdateProvider = ['icm-b2c', 'icm-b2x', 'versionfilter-icm', 'versionfilter-icm-test']
        defaultUpdate = 'MINOR'
    }
}

subprojects {
	apply plugin: 'com.intershop.gradle.buildinfo'
  
	project.plugins.withType(JavaBasePlugin) {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}

	version = rootProject.version
	group = 'com.ivable.common'

	// TODO use traditional build output dir until IS-12727 is resolved
	buildDir = new File(projectDir, 'target')

	apply plugin: 'intershop-base'
	apply plugin: 'com.intershop.gradle.javadoc-configuration'
    apply plugin: 'com.intershop.gradle.javasource-plugin'

	intershop {
		productName = 'Intershop'
		productCopyrightOwner = 'Intershop Communications AG'
	}
	
	// print detailed information about deprecated classes
	tasks.withType(JavaCompile) {
		options.deprecation = true
	}
	
	dependencies {
		// com.intershop.component.spreadsheet.capi.CellSelector.Cell<T extends ComputedItem> indirectly referenced through
		// com.intershop.beehive.bts.capi.orderprocess.LineItemCtnr (or a derived type like com.intershop.beehive.bts.capi.orderprocess.basket.Basket)
		compile group: 'com.intershop.platform', name: 'bc_spreadsheet'
		
		// com.intershop.beehive.pipeline.capi.ParameterizedPipelineElement
		compile group: 'com.intershop.platform', name: 'pipeline'

		compile  'javax.inject:javax.inject'
		compile  'org.slf4j:slf4j-api'
		// required for isml2classMain
		compile 'ch.qos.logback:logback-core'
		compile 'com.google.inject:guice'
		compile 'org.apache.tomcat:tomcat-el-api'
		compile 'javax.servlet:javax.servlet-api'
	}
}

buildscript {
    dependencies {
        //bintray dependencies
        classpath 'com.intershop.gradle.scm:scmversion-gradle-plugin:2.6.0'
        classpath 'com.intershop.gradle.publish:gradle-release-plugins:3.5.3'
        classpath 'com.intershop.gradle.versionrecommender:versionrecommender-gradle-plugin:1.3.4'
        classpath 'com.intershop.gradle.repoconfig:repoconfig-gradle-plugin:3.3.0' 
		 
        classpath 'com.intershop.gradle.resourcelist:resourcelist-gradle-plugin:2.1.0'
		classpath 'com.intershop.gradle.jaxb:jaxb-gradle-plugin:1.0.1'
		classpath 'com.intershop.gradle.isml:isml-gradle-plugin:1.0.4'
		classpath 'com.intershop.gradle.wsdl:wsdl-gradle-plugin:1.1.0'

        classpath 'com.intershop.build.gradle:ish-component-plugin:2.11.9'
        classpath 'com.intershop.build.gradle:hot-code-reloading:2.11.9'

        classpath "org.lesscss:lesscss:1.7.0.1.1"

        classpath 'com.intershop.deployment:assembly-toolbox:3.0.4' 
        classpath 'com.intershop.deployment:deploy-intershop:3.0.3'
    }
}
